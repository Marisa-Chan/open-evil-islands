#!/usr/bin/env python
# -*- encoding: utf-8 -*-

#  This file is part of Cursed Earth.
#
#  Cursed Earth is an open source, cross-platform port of Evil Islands.
#  Copyright (C) 2009-2015 Yanis Kurganov <ykurganov@users.sourceforge.net>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program. If not, see <http://www.gnu.org/licenses/>.

import os
import utils

import SCons

Import("env")

env = env.Clone()

env.PrependUnique(
    CPPPATH=["include", ".", os.path.join("build", "$GEN_PATH")],
)

# TODO proper build
env['ENV']['PATH'] = os.environ['PATH']

env.VariantDir(os.path.join("build", "$GEN_PATH"), "src", duplicate=False)

sources = [os.path.join("build", "$GEN_PATH", "eiscript.tab.cpp"),
    os.path.join("build", "$GEN_PATH", "lex.yy.cpp"),
    [os.path.join("build", "$GEN_PATH", node.name) for node in
    utils.filter_sources(env, env.Glob(os.path.join("src", "*.cpp")))]]


# TODO proper build
yaccTarget = env.Command(os.path.join("build", "$GEN_PATH", "eiscript.tab.cpp"), os.path.join("src",'eiscript.y'), "win_bison -o $TARGET -d --language=C++ $SOURCE")
lexTarget = env.Command(os.path.join("build", "$GEN_PATH", "lex.yy.cpp"), os.path.join("src",'eiscript.l'), "win_flex -o $TARGET $SOURCE")
library = env.Library(os.path.join("lib", "$GEN_PATH", "eiscript"), sources)

Depends(lexTarget, yaccTarget)
Depends(library, lexTarget)

targets = [env.Command(os.path.join("lib", SCons.Util.adjustixes("eiscript",
    env.subst("$LIBPREFIX"), env.subst("$LIBSUFFIX"))), library,
    Copy("$TARGET", "$SOURCE"))]

Return("targets")
